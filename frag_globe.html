<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    //for height map
    uniform vec3 low_color;
    uniform vec3 high_color;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);
    //random generator from http://www.java-gaming.org/index.php?topic=28018.0
    float rand(vec2 co){
        return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
    }


void main(void)
{
    // surface normal - normalized after rasterization
    vec3 normal = normalize(v_Normal);
    float night_flag=dot(v_Normal,u_CameraSpaceDirLight);
    // normalized eye-to-position vector in camera coordinates
    vec3 eyeToPosition = normalize(v_Position);
    //for specular map
    vec3 maskColor=texture2D(u_Bump,v_Texcoord).rgb;
    
    //bump map
    vec2 bump_center_coord=v_Texcoord;
    vec2 bump_right_coord=bump_center_coord+vec2(1.0/1024.0,0.0);
    vec2 bump_top_coord=bump_center_coord+vec2(0.0,1.0/512.0);
    float bump_center=texture2D(u_Bump,bump_center_coord).x;
    float bump_right=texture2D(u_Bump,bump_right_coord).x;
    float bump_top=texture2D(u_Bump,bump_top_coord).x;

    
    
    vec3 bump_normal=eastNorthUpToEyeCoordinates(v_positionMC,v_Normal)*normalize(vec3(bump_center-bump_right,bump_center-bump_top,0.15));
    
    float bump_diffuse=clamp(dot(-u_CameraSpaceDirLight, bump_normal), 0.0, 1.0);
    
    float diffuse = clamp(dot(-u_CameraSpaceDirLight, normal), 0.0, 1.0);
    
    vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
    float specular = max(dot(toReflectedLight, eyeToPosition), 0.0);
    specular = pow(specular, 20.0);
    
    float gammaCorrect = 1.0/1.3; //gamma correct by 1/1.2
    
    vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
    vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
    //apply gamma correction to nighttime texture
    nightColor = pow(nightColor,vec3(gammaCorrect));
    
    if(maskColor.r>0.0)
    {
        specular=0.0;
    }
    if(maskColor.r==0.0)
    {
        bump_diffuse+=(rand(v_Texcoord*u_time*0.1)+rand(v_positionMC.xy*u_time*0.1))*0.3;
    }
    vec3 day_color = ((0.9 * bump_diffuse) + (0.5 * specular)) * dayColor;
    //for height map
    float altitude=texture2D(u_Bump,v_Texcoord).r;
    if(altitude<0.5)
    {
        day_color=mix(day_color,low_color,0.1);
    }
    else
    {
        day_color=mix(day_color,high_color,0.1);
    }
    
    
    
    
    //for clouds
    vec2 v_cloud=v_Texcoord+vec2(u_time*0.25,0.0);
    //vec2 v_cloud=v_Texcoord;
    //v_cloud+=vec2(u_time,u_time);
    vec3 cloudColor=texture2D(u_Cloud,v_cloud).rgb;
    vec3 cloudTrans=texture2D(u_CloudTrans,v_cloud).rgb;
    //day cloud
    vec3 dayCloud=cloudColor;
    vec3 nightCloud=vec3(0.0,0.0,0.0);
    //mixed day and night cloud
    vec3 mixed_dayColor=mix(dayCloud*0.8,day_color,cloudTrans.r);
    vec3 mixed_nightColor=mix(nightCloud,nightColor,cloudTrans.r);
    
    vec3 color=mix(mixed_dayColor,mixed_nightColor,(night_flag+1.0)/2.0);
    float rimfactor=clamp(dot(v_Normal,v_Position)+1.0,0.0,0.5);
    if(rimfactor>0.0)
    {
        color+=vec3(rimfactor/4.0,rimfactor/2.0,rimfactor/2.0);
    }
    gl_FragColor = vec4(color, 1.0);
}

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0));
        // normalized surface tangent in eye coordiantes
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
</script>

<script src ="js/lib/gl-matrix.js" type ="text/javascript"></script>
<script src ="js/webGLUtility.js" type ="text/javascript"></script>
<script src ="js/frag_globe.js" type ="text/javascript"></script>
</body>

</html>
